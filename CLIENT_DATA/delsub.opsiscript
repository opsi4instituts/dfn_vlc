Message "Deinstalling " + $ProductName$ + " ..."

killtask "vlc.exe"

; Ergebnis ist StringList (mehrere Einträge möglich);
; in einer Zeile werden drei Ergebnisse mit "|" abgetrennt geliefert: Registrykey (mit Pfad) | Bereich Registry (kann direkt getRegistryValue übergeben werden | DisplayName
; z.B. HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Test64|64bit|Test Registry Entry64
Set $ResultList$ = SearchRegistryUninstallKeys($ProductId$)

Switch count ($ResultList$)
	Case "0"
		comment "No installations of " + $SearchPattern$ + " found in $RegPathUninstall$. Nothing to do ;-)"
	EndCase
	Case "1"
		set $firstInstallation$ = "false"

		Set $RegId$ = takeString (0,splitString(takeString(0,$ResultList$), '|'))
		Set $RegBitScope$ = takeString (1,splitString(takeString(0,$ResultList$), '|'))
		Set $UninstallProgram$ = GetRegistryValue($RegId$, "UninstallString", $RegBitScope$)
		; entferne Anführungszeichen aus RegKey UninstallString, falls vorhanden
		Set $UninstallProgram$ = stringReplace($UninstallProgram$, '"', '')
	EndCase
	DefaultCase
		logError "Found more than one installation! This case is not intended yet in this script! Stop now."
		isFatalError
	EndCase
EndSwitch

if not ( $UninstallProgram$ = "" )
   	Winbatch_uninstall
   	Set $ExitCode$ = getLastExitCode
	if stringToBool(isNsisExitcodeFatal($ExitCode$, "True", $ErrorString$))
        isFatalError
    endif   	
   	
	comment "include custom post deinstall file"
	set $CustomPostDeinstall$ = getProductProperty("custom-post-deinstall","none")
	if not ($CustomPostDeinstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostDeinstall$)
			include_insert "%ScriptPath%\custom\" + $CustomPostDeinstall$
		endif
	endif
endif

[Winbatch_uninstall]
"$UninstallProgram$" /S


