[Actions]
; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SYSTEMDRIVE%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError "No Space"
else
	if FileExists("%ScriptPath%\delsub.opsiscript")
		comment  "start uninstall sub section"
		sub "%ScriptPath%\delsub.opsiscript"
	endif

	Message "Installing %installingProdName% (%installingProdVersion%) " + $INST_architecture$ + "Bit..."
	
	comment "Start setup program"
	Winbatch_install
	Set $ExitCode$ = getLastExitCode
	if stringToBool(isNsisExitcodeFatal($ExitCode$, "True", $ErrorString$))
        isFatalError
    endif

	; Ergebnis ist StringList (mehrere Einträge möglich);
	; in einer Zeile werden drei Ergebnisse mit "|" abgetrennt geliefert: Registrykey (mit Pfad) | Bereich Registry (kann direkt getRegistryValue übergeben werden | DisplayName
	; z.B. HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Test64|64bit|Test Registry Entry64
	Set $ResultList$ = SearchRegistryUninstallKeys($ProductId$)

	Switch count ($ResultList$)
		Case "0"
			logError "Fatal: After Installation " + $ProductName$ + " could not be found in " + $RegPathUninstall$
			isFatalError
		EndCase
		Case "1"
			Set $RegId$ = takeString (0,splitString(takeString(0,$ResultList$), '|'))
			Set $RegBitScope$ = takeString (1,splitString(takeString(0,$ResultList$), '|'))

			; check version
			Set $InstalledVersion$ = GetRegistryValue($RegId$, "DisplayVersion", $RegBitScope$)
			if not ($ProductVersion$ = $InstalledVersion$)
				logError "Fatal: Versions (package <-> installed) does not match)"
				isFatalError
			endif
			comment "Successful Installation"
			
			comment "Get 'DisplayIcon' from registry for path to exe"
			Set $PathToExe$ = GetRegistryValue($RegId$, "DisplayIcon", $RegBitScope$)

			; Installer legt immer ein Desktop-Link an, daher umgedrehte Logik
			if $DesktopLink$ = "false"
				Linkfolder_delete_desktopicon
			endif  

			comment "include custom post install file"
			set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
			if not ($CustomPostInstall$ = "none")
				if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
					include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
				endif
			endif
			
			if $firstInstallation$ = "true"
				Registry_set_language /AllNTUserDats
			endif
		EndCase
	EndSwitch
endif

[Winbatch_install]
; === Nullsoft Scriptable Install System ================================================================
"%ScriptPath%\$InstFile$" /L=$language$ /S

[Registry_set_language]
openkey [HKEY_CURRENT_USER\Software\VideoLAN\VLC]
set "Lang" = REG_SZ:"$language$" 

[Linkfolder_delete_desktopicon]
	set_basefolder common_desktopdirectory
	set_subfolder ""
	delete_element $ProductName$

