[Actions]
; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SYSTEMDRIVE%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError "No Space"
else
	killtask "vlc.exe"

	if FileExists("%ScriptPath%\delsub.opsiscript")
		comment  "start uninstall sub section"
		sub "%ScriptPath%\delsub.opsiscript"
	endif

	Message "Installing %installingProdName% (%installingProdVersion%) " + $INST_architecture$ + "Bit..."
	
	comment "Start setup program"
	Winbatch_install
	Sub_check_exitcode
	
	Set $SearchPattern$ = $ProductName$
	; Parameter: $SearchPattern$ Suchbegriff in Registry
	if ($INST_architecture$ = "64")
		Sub_search_registry64_uninstall_keys
	else
		Sub_search_registry32_uninstall_keys
	endif
	; Rückgabewert: $ResultList$ gefundene Einträge

	Switch count ($ResultList$)
		Case "0"
			logError "Fatal: After Installation " + $ProductName$ + " could not be found in " + $RegPathUninstall$
			isFatalError
		EndCase
		Case "1"
			comment "Successful Installation"
			comment "Get 'DisplayIcon' from registry for path to exe"
			Set $RegId$ = takeSTring(0, $ResultList$)
			if ($INST_architecture$ = "64")
				Set $InstallDirExe$ = GetRegistryStringValue64("[" + $RegPathUninstall$ + "\" + $RegId$ + "] DisplayIcon")
			else
				Set $InstallDirExe$ = GetRegistryStringValue32("[" + $RegPathUninstall$ + "\" + $RegId$ + "] DisplayIcon")
			endif

			Set $DesktopIcon$ = GetProductProperty("desktopicon", "true")
			if $DesktopIcon$ = "false"
				Linkfolder_delete_desktopicon
			endif  

			comment "include custom post install file"
			set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
			if not ($CustomPostInstall$ = "none")
				if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
					include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
				endif
			endif
			
			if $firstInstallation$ = "true"
				Registry_set_language /AllNTUserDats
			endif
		EndCase
	EndSwitch
endif

[Winbatch_install]
; === Nullsoft Scriptable Install System ================================================================
"%ScriptPath%\$InstFile$" /L=$language$ /S

[Registry_set_language]
openkey [HKEY_CURRENT_USER\Software\VideoLAN\VLC]
set "Lang" = REG_SZ:"$language$" 

[Linkfolder_delete_desktopicon]
	set_basefolder common_desktopdirectory
	set_subfolder ""
	delete_element $ProductName$

